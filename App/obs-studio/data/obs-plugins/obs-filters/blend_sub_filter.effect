uniform float4x4 ViewProj;
uniform texture2d image;
uniform float4x4 color_matrix;
uniform float3 color_range_min = {0.0, 0.0, 0.0};
uniform float3 color_range_max = {1.0, 1.0, 1.0};

uniform texture2d target;
uniform float4 color;

sampler_state textureSampler {
	Filter    = Linear;
	AddressU  = Clamp;
	AddressV  = Clamp;
};

struct VertData {
	float4 pos : POSITION;
	float2 uv  : TEXCOORD0;
};

VertData VSDefault(VertData v_in)
{
	VertData vert_out;
	vert_out.pos = mul(float4(v_in.pos.xyz, 1.0), ViewProj);
	vert_out.uv  = v_in.uv;
	return vert_out;
}

float4 PSSubtractImageRGBA(VertData v_in) : TARGET
{
	float4 rgba = image.Sample(textureSampler, v_in.uv) * color;

	float4 targetRGB = target.Sample(textureSampler, v_in.uv);
	rgba.rgb = saturate(rgba.rgb - targetRGB.rgb);
	return rgba;
}

float4 PSSubtractImageMatrix(VertData v_in) : TARGET
{
	float4 yuv = image.Sample(textureSampler, v_in.uv);
	yuv.xyz = clamp(yuv.xyz, color_range_min, color_range_max);

	float4 rgba = saturate(mul(float4(yuv.xyz, 1.0), color_matrix)) *
		color;

	float4 targetRGB = target.Sample(textureSampler, v_in.uv);
	rgba.rgb = saturate(rgba.rgb - targetRGB.rgb);
	return rgba;
}

technique Draw
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader  = PSSubtractImageRGBA(v_in);
	}
}

technique DrawMatrix
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader  = PSSubtractImageMatrix(v_in);
	}
}
